plugins {
    id 'idea'
    id 'java-gradle-plugin'
    id 'maven-publish'

    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.palantir.git-version' version '0.5.3'
}

gradlePlugin {
    plugins {
        playLess {
            id = 'org.iatoki.play-less'
            implementationClass = 'org.iatoki.gradle.play.less.PlayLessPlugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.lesscss:lesscss:1.7.0.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.6.2'
}

group 'org.iatoki'
version gitVersion().substring(1)

sourceSets {
    integTest {
        java {
            srcDirs = ['src/integTest/java']
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']

    pkg {
        repo = 'releases'
        name = 'gradle-play-less'
        userOrg = 'ia-toki'

        version {
            name = project.version
        }
    }
}

idea.module {
    testSourceDirs += file('src/integTest/java')
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}
